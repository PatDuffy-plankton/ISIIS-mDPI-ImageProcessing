//This macro performs a particle analysis then crops the plankton
//from the original image and saves it to the segments directory
//Particle stats are saved to the ParticleCounts directory
//Adam Greer
//University of Southern Mississippi January 2016

dir = getDirectory("Choose a Directory");
filelist = getFileList(dir);
max = lengthOf(filelist);

for (i=0; i<max; i++)  {
open(dir+filelist[i]);
stacktitle = getTitle();
hh = getHeight();
ww = getWidth();
setAutoThreshold("Default");
//run("Threshold...") set this value or use a segmentation method
setThreshold(0, 170, "Black & White");

run("Set Measurements...", "area perimeter bounding fit shape stack display redirect = None decimal = 3");

//Set the size of the particles segmented here
run("Analyze Particles...", "size=2000-inf circularity=0-1 show=Nothing display clear include stack slice");

//save the particle stats
if(nResults > 0) {
saveAs("Results", "d:/ParticleCounts/"+filelist[i]+".txt");

for (j=0; j<nResults; j++) {
imagename = getResult("Label", j);
xpos = getResult("BX", j);
ypos = getResult("BY", j);
xlength = getResult("Width", j);
ylength = getResult("Height", j);
sslice = getResult("Slice", j);

//give the selection a little buffer zone
buffer = 20;
xpos2 = xpos - buffer;
xlength2 = xlength + (2*buffer);
ypos2 = ypos - buffer;
ylength2 = ylength + (2*buffer);

if(xpos2+xlength2 > hh)
	xlength2 = hh - xpos2;
else
	xlength2 = xlength2;

if(xpos2 < 0)
	xpos2 = 0;
else
	xpos2 = xpos2;

if(ypos2+ylength2 > ww)
	ylength2 = ww - ypos2;
else
	ylength2 = ylength2;


if(ypos2 < 0)
	ypos2 = 0;
else
	ypos2 = ypos2;


selectWindow(stacktitle);
setSlice(sslice);
makeRectangle(xpos2, ypos2, xlength2, ylength2);
run("Copy");
run("Internal Clipboard");
selectWindow("Clipboard");
//set where you want the segments to go here
saveAs("Tiff", "d:/Segments/Plankton_"+stacktitle+"_"+sslice+"_"+xpos+"_"+ypos+"_"+xlength+"_"+ylength); 
run("Close");

} //close the for loop
close();
selectWindow("Results");
run("Close");
} else { //close the if statement
close();
}
} //close massive for loop




